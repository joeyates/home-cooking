#######################
# programs
PATH=~/bin:/usr/local/bin:$PATH
PATH=$PATH:$HOME/.rvm/bin # Add RVM to PATH for scripting

#######################
# visual
#
# In prompt, show (at most) last three directories of path
PS1="%F{blue}%B%3d>%f%b"

#######################
# history
HISTFILE=~/.histfile
HISTSIZE=100000
SAVEHIST=100000
setopt INC_APPEND_HISTORY         # add new items as soon as they are entered
setopt HIST_IGNORE_ALL_DUPS       # when adding commands, remove previous occurences

#######################
# various
setopt AUTO_CD                    # if a command is not found but is identical to a directory, assume cd
setopt EXTENDED_GLOB              # Treat  the  `#',  `~' and `^' characters as part of patterns for
                                  # filename generation, etc.
unsetopt BEEP                     # do not beep on errors

#######################
# keyboard
bindkey -v                        # vim keybindings

# Complete backwards on history using Arrow Up/Down
autoload -Uz history-search-end
zle -N history-beginning-search-backward-end \
       history-search-end
zle -N history-beginning-search-forward-end \
       history-search-end
bindkey '\e[A' history-beginning-search-backward-end
bindkey '\e[B' history-beginning-search-forward-end


# completion
zstyle :compinstall filename $HOME/.zshrc
autoload -Uz compinit
compinit

########################
# source computer-specific configurations
if [ ${#$(print ~/.zshrc_*(.N))} -gt 0 ]; then
  for i in ~/.zshrc_*(.); . $i
fi

########################
# aliases
alias ls='ls --color=auto'
alias l='ls -l'
alias la='ls -lA'
alias lh='ls -lh'
alias v='vimx'
alias g='git'

# various functions
function f() { find $2 -iname "*$1*"; }
function jgy_screen_precmd() {
  # Keep screen up-to-date on the current directory
  if [[ "$TERMCAP" =~ 'screen' ]] then
    screen -X chdir "`pwd`"
  fi
}
autoload add-zsh-hook
type add-zsh-hook >/dev/null 2>&1 && add-zsh-hook precmd jgy_screen_precmd

########################
# rvm
[[ -s "$HOME/.rvm/scripts/rvm" ]] && . "$HOME/.rvm/scripts/rvm"

